options {
	JAVA_UNICODE_ESCAPE=true;
	STATIC=false;					// 1 seul parseur actif a la fois (+ rapide)
	MULTI=true;						// type noeud = f(nom noeud)
	VISITOR=true;					// Cree une methode accept(visiteur)
	NODE_USES_PARSER=true;
}

//
// PARSER DEFINITION
//

PARSER_BEGIN(Parser)

class Parser
{
	public static void main(String args[])
	{
		Parser c = new Parser(new java.io.DataInputStream(System.in));
		try {
			ASTProgram n = c.Program();
			ParserVisitor v = new PrintExpressionDAGVisitor(args[0]);
			n.jjtAccept(v, null);
			v = new PrintOptimizedCodeVisitor(args[1]);
			n.jjtAccept(v, null);
		} catch (Exception e) {
			System.out.println("Erreur: " + '\n' + e);
			System.exit(-1);
		}
	}
}

PARSER_END(Parser)

//
// LEXICAL ANALYSIS
//

// White space

SKIP :
{
	< ( " " | "\t" | "\n" | "\r" )+	>
}

// Comments

MORE :
{
  "//" : IN_SINGLE_LINE_COMMENT
|
  <"/**" ~["/"]> { input_stream.backup(1); } : IN_FORMAL_COMMENT
|
  "/*" : IN_MULTI_LINE_COMMENT
}

<IN_SINGLE_LINE_COMMENT>
SPECIAL_TOKEN :
{
  <SINGLE_LINE_COMMENT: "\n" | "\r" | "\r\n" > : DEFAULT
}

<IN_FORMAL_COMMENT>
SPECIAL_TOKEN :
{
  <FORMAL_COMMENT: "*/" > : DEFAULT
}

<IN_MULTI_LINE_COMMENT>
SPECIAL_TOKEN :
{
  <MULTI_LINE_COMMENT: "*/" > : DEFAULT
}

<IN_SINGLE_LINE_COMMENT,IN_FORMAL_COMMENT,IN_MULTI_LINE_COMMENT>
MORE :
{
  < ~[] >
}


// Operators
TOKEN :
{
	<ASSIGN: < EQUAL > > |
	< OPERATOR: < PLUS > | < MINUS > | < MULT > | < DIV > > |
	< UNARY_MINUS: "minus" > |

	< #PLUS: "+" > |
	< #MINUS: "-" > |
	< #MULT: "*" > |
	< #DIV: "/" > |

	< #EQUAL: "=" >
}


// Identifiers and numbers
TOKEN :
{
	< IDENTIFIER: <LETTER> ( <LETTER> | <DIGIT> )* > |
	< #LETTER: ["a"-"z","A"-"Z","_"] > |
	< #DIGIT: ["0"-"9"] > |
	< INTEGER: ["1"-"9"] (["0"-"9"])* | "0" >
}




//
// SYNTAX ANALYSIS
//

ASTProgram Program() : { }
{
	[Live()]
	Block() <EOF> { return jjtThis;  }
}

void Live() : { Token t;}
{
	"LIVE:" (t = <IDENTIFIER> {jjtThis.addLive(t.image);})+ ";"
}

void Block() : { }
{
	 ( Stmt() )*
}

void Stmt() : { }
{
	LOOKAHEAD(4) AssignStmt() |
	LOOKAHEAD(4) AssignUnaryStmt() |
	LOOKAHEAD(4) AssignDirectStmt()
}

void AssignStmt() : { Token t;}
{
	Identifier() <ASSIGN> Expr() t = <OPERATOR> {jjtThis.setOp(t.image);} Expr()
}

void AssignUnaryStmt() : { Token t;}
{
	Identifier() <ASSIGN> <UNARY_MINUS> Expr()
}

void AssignDirectStmt() : { Token t;}
{
	Identifier() <ASSIGN> Expr()
}

void Expr() : {}
{
	Identifier() |
	IntValue()
}

void Identifier() : { Token t;}
{
	t = <IDENTIFIER> {jjtThis.setValue(t.image);}
}

void IntValue() : { Token t;}
{
	t = <INTEGER> {jjtThis.setValue(Integer.parseInt(t.image));}
}
